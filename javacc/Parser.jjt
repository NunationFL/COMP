PARSER_BEGIN(Parser)

import pt.up.fe.comp.jmm.report.Report;
import pt.up.fe.comp.jmm.report.Stage;
import pt.up.fe.comp.jmm.report.ReportType;

public class Parser
{
    public static int errorLimit = 10;
    public static int errorCounter = 0;
}

PARSER_END(Parser)

SKIP:
{
    " " | "\n" | "\t" | "\r"
}

TOKEN:
{
    < INTEGERLITERAL : (["0"-"9"])+ >
    | < IF : "if">
    | < INT : "int">
    | < NEW : "new">
    | < VOID : "void">
    | < ELSE : "else">
    | < TRUE : "true">
    | < MAIN : "main">
    | < THIS : "this">
    | < WHILE : "while">
    | < FALSE : "false">
    | < CLASS : "class">
    | < PUBLIC : "public">
    | < STATIC : "static">
    | < STRING : "String">
    | < LENGTH : "length">
    | < RETURN : "return">
    | < IMPORT : "import">
    | < BOOLEAN : "boolean">
    | < EXTENDS : "extends">
    | < OPENPAR : "(">
    | < CLOSEPAR : ")">
    | < SEMICOLON : ";">
    | < OPENCUR : "{">
    | < IDENTIFIER : (["a"-"z","A"-"Z"])(["a"-"z","A"-"Z","0"-"9","_"])* >
}

JAVACODE
void pushErrorNode() #void
{
    jjtree.pushNode(new SimpleNode(48));
}

JAVACODE
void handleError(ParseException e) #void
{
    addReportToMain("Error found in while expression!", ReportType.WARNING);
    error_skipto("while expression");
    errorCounter++;
    pushErrorNode();
    if (errorCounter == errorLimit)
    {
        addReportToMain("Error found in while expression!", ReportType.ERROR);
        throw e; 
    }  
}

JAVACODE
void error_skipto(String description) #void {
    ParseException e = generateParseException();  // generate the exception object
    Token t;

    t = getToken(1);
    System.out.println("Unexpected \"" + t.image + "\" in " + description + " at line " + t.beginLine + "!");

    while(t.kind != SEMICOLON && t.kind != EOF && t.kind != OPENCUR && t.kind != CLOSEPAR)
    {
        t = getNextToken();
        t = getToken(1);
    }

}

JAVACODE
void addReportToMain(String message, ReportType reportType) #void
{
    Token t = getToken(1);
    int lineError = t.beginLine;
    Stage stage = Stage.SYNTATIC;
    Main.reports.add(new Report(reportType, stage, lineError, message));
}

SimpleNode Program() : {Token t1;}
{
    ImportDeclaration() Class() <EOF> {return jjtThis;}
}

void ImportDeclaration() : {}
{
    (ImportMaterial())*
}

void ImportMaterial() : {Token t1,t2;}
{
    <IMPORT> t1=<IDENTIFIER> ("." t2=<IDENTIFIER> {jjtThis.name += "." + t2.image;})* {jjtThis.name = t1.image + jjtThis.name;} <SEMICOLON>
}

void Class() : {Token t1;}{
    <CLASS> (t1=<IDENTIFIER> {jjtThis.name = t1.image;} ) ClassDeclaration()
}

void ClassDeclaration() #void: {Token t2;}
{
     (<EXTENDS> t2=<IDENTIFIER> {jjtThis.name = t2.image;} #ExtendedClassName)? <OPENCUR> (VarDeclaration())* (MethodDeclaration())* "}"
}

void VarDeclaration() : {Token t1,t2;}
{
   t2=Type() {jjtThis.type = t2.image;} #VarType t1=<IDENTIFIER> {jjtThis.name = t1.image;} #VarName <SEMICOLON>
}

void MethodDeclaration() #void : {}
{
    <PUBLIC> (NormalMethodDeclaration() | MainMethodDeclaration())
}

void NormalMethodDeclaration() : {Token t1,t2;}
{
    (t2=Type() {jjtThis.type = t2.image;} #ReturnType) t1=<IDENTIFIER> {jjtThis.name = t1.image;} #MethodName <OPENPAR> (MethodDeclarationArgs())? <CLOSEPAR> <OPENCUR> (LOOKAHEAD(2) VarDeclaration())* (Statement())* <RETURN> Expression() #ReturnValue <SEMICOLON> "}"
}

void MainMethodDeclaration() : {Token t1;}
{
    <STATIC> <VOID> <MAIN> <OPENPAR> <STRING> "[" "]" t1=<IDENTIFIER> {jjtThis.name = t1.image;} #ClArgsName <CLOSEPAR> <OPENCUR> (LOOKAHEAD(2) VarDeclaration())* (Statement())* "}" 
}

void MethodDeclarationArgs() : {Token t1,t2,t3,t4;}
{
    ((t3=Type() {jjtThis.type = t3.image;} #ParameterType) (t1=<IDENTIFIER> {jjtThis.name = t1.image;} #ParameterName) #Parameter(2)) ("," ((t4=Type() {jjtThis.type = t4.image;} #ParameterType) (t2=<IDENTIFIER> {jjtThis.name = t2.image;} #ParameterName) #Parameter(2)) )* 
}

Token Type() #void : {Token t1,t2,t3;}
{
    t1=<INT> ("[" "]" {t1.image += "[]";})? {return t1;}
    | t2=<BOOLEAN> {return t2;}
    | t3=<IDENTIFIER> {return t3;}
}

void Body() : {}
{
    Statement()
}

void Statement() #void : {Token t1;} 
{
    <OPENCUR> (Statement() )* "}"
    | <IF> <OPENPAR> Expression() <CLOSEPAR> Body()  #If(2) <ELSE> Statement() #Else
    | <WHILE> <OPENPAR> 
   try 
   {
        Expression()
   }
   catch(ParseException e)
   {
        handleError(e);
   } <CLOSEPAR> Body() #While(2)
    | Expression3() <SEMICOLON>
    | (t1=<IDENTIFIER> {jjtThis.name = t1.image;} #AssignRecipient) (
        "=" Assignee() #Assign(2) 
            | "[" Expression() #ArrayIndex "]" ("=" Expression() #ArrayAssign(3) <SEMICOLON> | ExpressionLFRFix())
            | ExpressionLFRFix2() <SEMICOLON> )
}

void Assignee() #void : {}
{
    Expression() <SEMICOLON>
}

void Expression() #void : {Token t1,t2;}
{
    t2=<INTEGERLITERAL> {jjtThis.numericalValue = t2.image;} #IntegerLiteral ExpressionLFRFix()
    | <TRUE> #True ExpressionLFRFix()
    | <FALSE> #False ExpressionLFRFix()
    | t1=<IDENTIFIER> {jjtThis.name = t1.image;} #VariableName ExpressionLFRFix()
    | <THIS> #This ExpressionLFRFix()
    | <NEW> (ArrayInitializer() | ConstructorCall()) ExpressionLFRFix()
    | "!" Expression() #Negation ExpressionLFRFix()
    | <OPENPAR> Expression() <CLOSEPAR> ExpressionLFRFix()
}

void Expression3() #void : {Token t1;}
{
    t1=<INTEGERLITERAL> {jjtThis.numericalValue = t1.image;} #IntegerLiteral ExpressionLFRFix()
    | <TRUE> #True ExpressionLFRFix()
    | <FALSE> #False ExpressionLFRFix()
    | <THIS> #This ExpressionLFRFix()
    | <NEW> (ArrayInitializer() | ConstructorCall()) #New ExpressionLFRFix()
    | "!" Expression() #Negation ExpressionLFRFix()
    | <OPENPAR> Expression() <CLOSEPAR> ExpressionLFRFix() 
}

void ArrayInitializer() : {}
{
    <INT> "[" Expression() "]"
}

void ConstructorCall() : {Token t1;}
{
    t1=<IDENTIFIER> {jjtThis.name = t1.image;} #NewObjectClassName <OPENPAR> <CLOSEPAR>
}

void ExpressionLFRFix() #void : {}
{
    (LOOKAHEAD(2) (Operation() ExpressionLFRFix()
    | "[" Expression() "]" ExpressionLFRFix()
    | "." ( MethodCall() | AttributeCall() ExpressionLFRFix())  ))?
}

void ExpressionLFRFix2() #void : {}
{
    (Operation() ExpressionLFRFix()
    | "." ( MethodCall() | <LENGTH> #Length ExpressionLFRFix()  ))?
}

void MethodCall() #void : {Token t1;}
{
    t1=<IDENTIFIER> {jjtThis.name = t1.image;} #MethodName <OPENPAR> (MethodCallArgs())? <CLOSEPAR> ExpressionLFRFix() #MethodCall(3)
}

void AttributeCall() #void : {}
{
    <LENGTH> #AttributeCall(1)
}

void MethodCallArgs() : {}
{
    Expression() ( "," Expression())*
}

void Operation() #void : {}
{
    "&&" Expression() #And(2)
    | "<" Expression() #SmallerThan(2)
    | "+" Expression() #Addition(2)
    | "-" Expression() #Subtraction(2)
    | "*" Expression() #Multiplication(2)
    | "/" Expression() #Division(2)
}
